# Contributor: DonVla <donvla@users.sourceforge.net>
#              Reza Jelveh <reza.jelveh at tuhh.de>

_pkgbasename=caps
pkgname=lib32-${_pkgbasename}
pkgver=0.4.5
pkgrel=1
pkgdesc="The C* Audio Plugin Suite (32 bit)"
arch=('x86_64')
url="http://quitte.de/dsp/caps.html"
license=('GPL')
options=(docs)
depends=('lib32-gcc-libs')
optdepends=('python2: optional CFLAGS check')
source=(http://quitte.de/dsp/${_pkgbasename}_${pkgver}.tar.gz
	'fix-lib32-makefile.patch')
md5sums=('f3fda0eec716202b658cc6075daa35d0'
	 '63103dfeb432e19c6de631f7cab843ca')
sha256sums=('cba5a7785ab5b1c596250fded999215906bc7eda7c8a688f677de15698e31616'
	    'a2d644100121d30db2744434f9a13ba4e4e54c1b7c6bbb62646f7a72f3a343b0')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  type -p python2 1>/dev/null && python2 configure.py
  patch -Np1 -i "${srcdir}/fix-lib32-makefile.patch"
  make
 }
 
package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  install -v -m 0755 -D caps.so ${pkgdir}/usr/lib32/ladspa/caps.so
}
